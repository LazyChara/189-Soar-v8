import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'eclipse'
	id 'com.gradleup.shadow' version '9.0.0-beta4'
}

group = 'com.soarclient'
version = '1.0-SNAPSHOT'

project.ext.lwjglVersion = "3.3.6"

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		def osArch = System.getProperty("os.arch")
		if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
			project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
		} else if  (osArch.startsWith("ppc")) {
			project.ext.lwjglNatives += "-ppc64le"
		} else if  (osArch.startsWith("riscv")) {
			project.ext.lwjglNatives += "-riscv64"
		}
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
		break
	case OperatingSystem.WINDOWS:
		def osArch = System.getProperty("os.arch")
		project.ext.lwjglNatives = osArch.contains("64")
			? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
			: "natives-windows-x86"
		break
}

repositories {
    maven { url 'https://www.jitpack.io' }
	maven { url "https://repo.viaversion.com" }
	maven { url "https://maven.lenni0451.net/everything"}
	maven { url 'https://repo.opencollab.dev/maven-snapshots/' }
	maven { url 'https://libraries.minecraft.net' }
    mavenCentral()
}

dependencies {

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl-nfd"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nfd::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"

	implementation 'it.unimi.dsi:fastutil:8.5.14'
	implementation 'org.joml:joml:1.10.8'
	implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
	implementation 'org.java-websocket:Java-WebSocket:1.5.7'

	implementation('net.raphimc:MinecraftAuth:4.1.1') {
		exclude group: "com.google.code.gson", module: "gson"
		exclude group: "org.slf4j", module: "slf4j-api"
    }

	implementation('com.viaversion:vialoader:4.0.0') {
		exclude group: "com.google.guava", module: "guava"
		exclude group: "org.slf4j", module: "slf4j-api"
    }

	implementation "com.viaversion:viaversion-common:5.2.1"
	implementation "com.viaversion:viabackwards-common:5.2.1"
	implementation "com.viaversion:viarewind-common:4.0.5"

	implementation 'io.github.humbleui:types:0.2.0'

	implementation 'com.google.code.gson:gson:2.11.0'
	implementation "com.google.guava:guava:33.4.0-jre"
	implementation 'commons-codec:commons-codec:1.17.2'
	implementation 'org.apache.commons:commons-compress:1.27.1'
	implementation 'commons-io:commons-io:2.18.0'
	implementation 'org.apache.commons:commons-lang3:3.3.2'
	implementation 'commons-logging:commons-logging:1.1.3'
	implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
	implementation 'org.apache.httpcomponents.core5:httpcore5:5.3.1'
	implementation 'org.apache.httpcomponents.client5:httpclient5:5.4.1'
	implementation 'org.apache.commons:commons-text:1.13.0'
	implementation 'io.netty:netty-all:4.1.116.Final'
	implementation 'org.jetbrains:annotations:26.0.1'

	implementation fileTree('libs')
}

jar {
    manifest {
        attributes "Main-Class" : "com.soarclient.launch.Launch"
    }
}